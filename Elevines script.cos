**CAOS2PRAY
*# Pray-File "elevines.agents"
*# DS-Name "Elevines"
*# Depend blnk.c16
*# attach elevinehelp.catalogue elevines-precolored.c16
*# desc = "An easy-to-use custom elevator system. Read the help file included for details. Lovely sprites by Mea."
*# Agent Animation File = "elevines-precolored.c16"
*# Agent Sprite First Image = 4
*# Agent Animation Gallery = "elevines"
*# Web URL = "naturingnurturing.blogspot.com"
*# Web Label = "Naturing :: Nurturing"



*lift teleporting ball things.
new: comp 3 1 21051 "elevines-precolored" 91 4 2000

*Inject it where it belongs:
setv va00 game "CreatorX"
setv va01 game "CreatorY"
doif va00 eq 0 and va01 eq 0
	setv va00 6110
	setv va01 9200
endi
doif tmvt va00 va01 eq 1
	mvto va00 va01
else
	mvsf va00 va01
endi

clac 1000

bhvr 3
*activate 1 (1) activate 2 (2)

tick 30


attr 198
*mouse pickupable (2) mouseclickable (4) collisions (64) physics (128)

accg 30
elas 20
fric 100
perm 100

*figure out what you look like!:
setv ov96 -1
*set the max at 200 loops (we'll come up with a better system for this someday)
setv va99 200

seta va98 targ

loop
	subv va99 1
	setv va00 rand 0 90
*make sure there's not already a system that looks like you:
	setv va01 0
	enum 3 1 21051
		doif va00 eq ov96
			setv va01 1
		endi
	next
	targ va98
*if you came out clean, congrats, you have a pose (and ID number):
	doif va01 eq 0
		setv ov96 va00
		sets ov00 vtos va00
		part 0
		pose ov96
	endi
untl va99 lt 0 or ov96 ge 0

*random tints are last resort now..
doif ov96 eq -1

*ov98 and 99 are going to be the random tints, ov97 is the random -pose-
	setv ov96 rand 0 12
	setv ov97 rand 50 200
	setv ov98 rand 50 200
	setv ov99 rand 50 200
	pose ov96
	tint ov97 ov98 ov99 128 128

*We'll base your ID number off your tints, just for fun:
	sets ov00 vtos ov97
	adds ov00 vtos ov98
	adds ov00 vtos ov99

endi

dbg: outs "this new elevine ID is:"
dbg: outs ov00

*parts-- 1, top button, 2, bottom
pat: dull 1 "elevines-precolored" 0 6 21 1
pat: dull 2 "elevines-precolored" 2 12 72 1


*Push -- Want to go up
scrp 3 1 21051 1
	inst
	lock
*first off, is it actually possible to go up?

*check where you are
	setv ov10 posy
*set the little serchagent to null
	seta va01 null
*enum through other circles
	enum 3 1 21051

*only check ones of your code
		doif mv00 eq ov00



*see if it's on a higher level-- that is, the y coord is less than mv10
			doif posy lt mv10
*herein lies the tricky part-- you want it to be lower than the ring
*being pushed, but higher than the rest.
*so NOW check to see if a previous agent has been found
				doif va01 eq null
*if not, make it the current favorite
* so set the ring you just found into va01
* and the y coord into va02

					seta va01 targ
					setv va02 posy
*else, you have to check it against the current va02 to
*make sure it's the highest-lower y coord
				else
					doif posy gt va02
						seta va01 targ
						setv va02 posy
					endi
				endi
			endi
		endi
	next
*So va01 should be the ideal ring to tele to now
*assuming there is one.
*if there's NOT, stop this whole thing now!
	doif va01 eq null

*make sure there's a FROM creature
		doif from ne null
*who pushed you?
			seta va00 from
			targ va00
			doif fmly = 4
*put a bug in it's ear-- stop pushing this!

				urge writ targ 26 -2 -1 2
				sndc "buzz"
*disappointment
				stim writ va00 0 1
				stop
			else
				stop

			endi
		else
			stop
		endi
*else there IS a va01!		
	else
*if it's being carried (like by the hand), stop
		targ va01
		doif movs ne 0
			stop
		endi
*now, make sure there's a creature to move
		doif from ne null
			seta va00 from
			targ va00
			doif fmly = 4
*set the coords of va01 into va09 and va10
				targ va01
				setv va09 posx
				setv va10 posb
				subv va10 1
*Lock it out from creatures for a second!
				tick tick
				attr 214


*now targ the norn
				targ va00
*make sure it's not being carried
				doif movs ne 0
					stop
				endi
*Make sure it's a VALID place to move it!
				doif tmvf va09 va10 = 1
*and move it!
					mvft va09 va10

*tell the norn it just travled
					stim writ targ 94 1
*and it needs to wait?
					stim writ va00 75 1
*tell the norn to STOP now..

					urge writ targ 26 -2 -1 2
*and make some dingysound
					sndc "snap"

				else
*if it's not a VALID PLACE make a sndc
					sndc "buzz"
					stop
				endi
			else
				stop
			endi

		endi
	endi
endm

*Pull -- Want to go down
scrp 3 1 21051 2
	inst
*first off, is it actually possible to go DOWN?

*check where you are
	setv ov10 posy
*set the little serchagent to null
	seta va01 null
*enum through other circles
	enum 3 1 21051

*only check ones of your code
		doif mv00 eq ov00



*see if it's on a higher level-- that is, the y coord is GREATER than mv10
			doif posy gt mv10
*herein lies the tricky part-- you want it to be lower than the ring
*being pushed, but higher than the rest.
*so NOW check to see if a previous agent has been found
				doif va01 eq null
*if not, make it the current favorite
* so set the ring you just found into va01
* and the y coord into va02

					seta va01 targ
					setv va02 posy
*else, you have to check it against the current va02 to
*make sure it's the lowest-higher y coord
				else
					doif posy lt va02
						seta va01 targ
						setv va02 posy
					endi
				endi
			endi
		endi
	next
*So va01 should be the ideal ring to tele to now
*assuming there is one.
*if there's NOT, stop this whole thing now!
	doif va01 eq null

*make sure there's a FROM creature
		doif from ne null
*who pushed you?
			seta va00 from
			targ va00
			doif fmly = 4
*put a bug in it's ear-- stop pulling this!

				sndc "buzz"

				urge writ targ 26 -2 -1 2
*disappointment
				stim writ va00 0 1
				stop
			else
				stop

			endi
		else
			stop
		endi
*else there IS a va01!		
	else
*if it's being carried (like by the hand), stop
		targ va01
		doif movs ne 0
			sndc "buzz"
			stop
		endi
*now, make sure there's a creature to move
		doif from ne null
			seta va00 from
			targ va00
			doif fmly = 4
*set the coords of va01 into va09 and va10
				targ va01
				setv va09 posx
				setv va10 posb
				subv va10 1
*Lock it out from creatures for a second!
				tick tick
				attr 214


*now targ the norn
				targ va00
*make sure it's not being carried
				doif movs ne 0
					stop
				endi
*Make sure it's a VALID place to move it!
				doif tmvf va09 va10 = 1
*and move it!
					mvft va09 va10
*tell the norn it just travled
					stim writ targ 94 1
*tell the norn to STOP now..
					urge writ targ 26 -2 -1 2
*wait?
					stim writ va00 75 1
*and make some dingysound
					sndc "snap"


				else
*if it's not a VALID PLACE make a sndc
					sndc "buzz"
					stop
				endi
			else
				stop
			endi

		endi
	endi
endm


*you've been CLICKED or clac'd or something
scrp 3 1 21051 1000
	inst
*where are you?
	setv va20 posl
	setv va21 post
*Here is how it works....
*shift+click creates a copy of the same OV
*ctrl+click creates a copy of a new OV
*shift+click kills it.
	sndc "opn1"
*shift -- make a copy, save OV, same tint
	doif keyd 16 eq 1 and keyd 17 eq 0
		sndc "opn1"

		new: comp 3 1 21051 "elevines-precolored" 91 4 2000

*move it to the old one, and make it hop a bit
		mvsf va20 va21
		velo 15 100

		clac 1000

		bhvr 3
*activate 1 (1) activate 2 (2)
		tick 30

		attr 198
*mouse pickupable (2) mouseclickable (4) collisions (64) physics (128)

		accg 30
		elas 20
		fric 100
		perm 100


*ov97, 96, 98 and 99 are going to be COPIED
		setv ov96 mv96
		setv ov97 mv97
		setv ov98 mv98
		setv ov99 mv99
		pose ov96
		doif ov97 gt 0 or ov98 gt 0 or ov99 gt 0
			tint ov97 ov98 ov99 128 128
		endi

*We'll base your ID number off your tints, just for fun:
		sets ov00 mv00

*parts-- 1, top button, 2, bottom
		pat: dull 1 "elevines-precolored" 0 6 21 1
		pat: dull 2 "elevines-precolored" 2 12 72 1


*ctrl
	elif keyd 16 eq 0 and keyd 17 eq 1
		sndc "unlk"
*lift teleporting ball things.
		new: comp 3 1 21051 "elevines-precolored" 91 4 2000

*move it to the old one, and make it hop a bit
		mvsf va20 va21
		velo 15 100
		clac 1000

		bhvr 3
*activate 1 (1) activate 2 (2)
		tick 30

		attr 198
*mouse pickupable (2) mouseclickable (4) collisions (64) physics (128)

		accg 30
		elas 20
		fric 100
		perm 100

*figure out what you look like!:
		setv ov96 -1
*set the max at 200 loops (we'll come up with a better system for this someday)
		setv va99 200

		seta va98 targ

		loop
			subv va99 1
			setv va00 rand 0 90
*make sure there's not already a system that looks like you:
			setv va01 0
			enum 3 1 21051
				doif va00 eq ov96
					setv va01 1
				endi
			next
			targ va98
*if you came out clean, congrats, you have a pose (and ID number):
			doif va01 eq 0
				setv ov96 va00
				sets ov00 vtos va00
				part 0
				pose ov96
			endi
		untl va99 lt 0 or ov96 ge 0

*random tints are last resort now..
		doif ov96 eq -1

*ov98 and 99 are going to be the random tints, ov97 is the random -pose-
			setv ov96 rand 0 12
			setv ov97 rand 50 200
			setv ov98 rand 50 200
			setv ov99 rand 50 200
			pose ov96
			tint ov97 ov98 ov99 128 128

*We'll base your ID number off your tints, just for fun:
			sets ov00 vtos ov97
			adds ov00 vtos ov98
			adds ov00 vtos ov99

		endi

		dbg: outs "this new elevine ID is:"
		dbg: outs ov00


*parts-- 1, top button, 2, bottom
		pat: dull 1 "elevines-precolored" 0 6 21 1
		pat: dull 2 "elevines-precolored" 2 12 72 1

*both
	elif keyd 16 eq 1 and keyd 17 eq 1
		kill ownr
	endi
endm

*Meh. I tried to avoid using a timer for this but it's just
*SO MUCH MORE FLUID with it. ]:
scrp 3 1 21051 9
	inst

*reset the attr if it's been changed
	attr 198
*mouse pickupable (2) mouseclickable (4) collisions (64) physics (128)

*the indicator orbs change color. Yay.
	setv va30 0
	setv va31 0
*check where you are
	setv ov10 posy
*enum through other circles
	enum 3 1 21051

*only check ones of your code
		doif mv00 eq ov00

*see if it's on a higher level-- that is, the y coord is less than mv10
			doif posy lt mv10
				setv va30 1
*lower?
			elif posy gt mv10
				setv va31 1
			endi
		endi
	next
*pose accordingly!
	part 1
	pose va30
	part 2
	pose va31


endm



rscr
enum 3 1 21051

	kill targ
next